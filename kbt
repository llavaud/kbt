#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use Getopt::Long qw/GetOptions HelpMessage/;
use HTTP::Request;
use IO::File;
use JSON qw/to_json from_json/;
use LWP::UserAgent;

GetOptions(
    'file=s'    => \(my $file = 'kbt_export.json'),
    'help'      => sub { HelpMessage(0) },
    'host=s'    => \(my $host = 'localhost:9200'),
    'index=s'   => \(my $index = '.kibana'),
    'output=s'  => \(my $output = 'kbt_export.json'),
    'overwrite' => \(my $overwrite = 0),
    'type=s'    => \(my $type = 'all'),
) or HelpMessage(1);

# add default port if missing
$host =~ s/^([\w\-\.]+)(?!\:\d+)$/$1:9200/;

my $cmd = $ARGV[0];

# params check
HelpMessage(1) if @ARGV != 1;
HelpMessage(1) if $type !~ /^(?:all|search|visualization|dashboard)$/;
HelpMessage(1) if $host !~ /^[\w\-\.]+\:\d+$/;

if ($cmd eq 'list') {
    &list(0);
}
elsif ($cmd eq 'export') {
    &export();
}
elsif ($cmd eq 'import') {
    &import();
}
else {
    HelpMessage(1);
}

exit 0;

sub list {
    my $export = shift;

    my $ctype;
    my $cid;
    my $ids = { 'docs' => [] };

# define list output format
format STDOUT_TOP =
Type            ID
-------------   ----------------------------------------------------------------
.
format STDOUT =
@<<<<<<<<<<<<   @<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<...
$ctype, $cid
.

    foreach my $t ('search', 'visualization', 'dashboard') {

        next if ($type ne 'all' and $t ne $type);

        my $ua = LWP::UserAgent->new;
        my $request = HTTP::Request->new(POST => "http://$host/$index/$t/_search");
        $request->content('{"query":{"match_all":{}}}');
        my $response = $ua->request($request);

        if ($response->is_success) {
            my $json = from_json($response->decoded_content);
            foreach my $doc (sort { $a->{'_id'} cmp $b->{'_id'} } @{ $json->{'hits'}->{'hits'} }) {
                if ($export == 0) {
                    $ctype = $t;
                    $cid = $doc->{'_id'};
                    write STDOUT;
                }
                elsif ($export == 1) {
                    push @{ $ids->{'docs'} }, { '_id' => $doc->{'_id'}, '_type' => $t };
                }
            }
        }
        else {
            print STDERR "Failed to get \'$t\' id(s): ".$response->status_line."\n";
        }
    }

    return ($export == 1) ? $ids : 1; 
}

sub export {
    my $ids = &list(1);

    # no documents to export
    return 1 if @{ $ids->{'docs'} } < 1;

    my $json = to_json($ids);

    my $ua = LWP::UserAgent->new;
    my $request = HTTP::Request->new(POST => "http://$host/$index/_mget");
    $request->content($json);
    my $response = $ua->request($request);

    if ($response->is_success) {
        my $fh;
        unless ($fh = IO::File->new($output, q{>})) {
            die "Failed to open file \'$output\': $!\n";
        }

        my $res = from_json($response->decoded_content);
        my $export = [];
        foreach my $doc (@{ $res->{'docs'} }) {
            delete $doc->{'found'};
            delete $doc->{'_version'};
            delete $doc->{'_index'};
            push @{ $export }, $doc;
        }
        print $fh to_json($export);
        close $fh;
    }
    else {
        print STDERR "Failed to export documents: ".$response->status_line."\n";
    }

    return 1;
}

sub import {
    my $file_json;
    if (my $fh = IO::File->new($file, q{<})) {
        $file_json = <$fh>;
        close $fh;
    } else {
        die "Failed to open file \'$file\': $!\n";
    }

    my $import = from_json($file_json);

    my $create = $overwrite ? '' : '/_create';

    foreach my $doc (@{ $import }) {
        next if ($type ne 'all' and $doc->{'_type'} ne $type);

        my $ua = LWP::UserAgent->new;
        my $request = HTTP::Request->new(POST => "http://$host/$index/$doc->{'_type'}/$doc->{'_id'}$create");
        $request->content(to_json($doc->{'_source'}));
        my $response = $ua->request($request);
        print STDERR "Failed to import \'$doc->{'_id'}\' ($doc->{'_type'}): ".$response->status_line."\n"
            if (!$response->is_success and $response->code != 409);
    }

    return 1;
}

__END__

=encoding utf8

=head1 NAME

kbt - Kibana Backup Tool

=head1 DESCRIPTION

This script allow to list, export and import kibana resources (search, visualization and dashboard).

=head1 SYNOPSIS

kbt [OPTIONS] <COMMAND>

 OPTIONS
   --file       FILE         Backup file to import (defaults to kbt_export.json)
   --help                    Print this help
   --host       IP[:PORT]    Ip address of elasticsearch instance (defaults to localhost:9200)
   --index      INDEX        Kibana index (defaults to .kibana)
   --output     FILE         Backup file (defaults to kbt_export.json)
   --overwrite               Overwrite existing documents during import (default is to skip existing docs)
   --type       TYPE         Type of kibana resources {search|visualization|dashboard} (defaults to all)

 COMMAND
   list                      list resource's id
   export                    export resources
   import                    import resources

=head1 AUTHOR

Laurent Lavaud

=head1 VERSION

0.5

=cut
